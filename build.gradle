buildscript {
    ext.developerFullName = 'Samet Kurumahmut'
    ext.developerEmail = 'samet.kurumahmut@gmail.com'
    ext.developerBintrayUserName = 'sametkurumahmut'
    ext.developerGithubUserName = 'sametkurumahmut'
    ext.developerGithubProjectName = 'kotlin-mdl-js'

    ext.projectName = 'kotlin-mdl-js'
    ext.projectDescription = 'Kotlin Material Design Lite - A Kotlin library which is a wrapper for Material Design Lite provides DSL to build HTML for web development using Kotlinx.html library.'
    ext.projectGroup = 'com.sametkurumahmut.kotlinx'
    ext.projectArtifact = 'kotlin-mdl-js'
    ext.projectVersion = '0.1.0'
    ext.projectLicense = 'The Apache Software License, Version 2.0'
    ext.projectLicenseTag = 'Apache-2.0'
    ext.projectLicenseUrl = 'https://www.apache.org/licenses/LICENSE-2.0'

    ext.kotlinVersion = '1.2.50'

    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "com.jfrog.bintray" version "1.8.2"
}

group projectGroup
version projectVersion

apply plugin: 'kotlin2js'
apply plugin: 'maven'
apply plugin: 'maven-publish'

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            groupId projectGroup
            artifactId projectArtifact
            version projectVersion
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = Closure.DELEGATE_FIRST
                    name projectName
                    description projectDescription
                    url "https://github.com/$developerGithubUserName/$developerGithubProjectName"
                    scm {
                        url "https://github.com/$developerGithubUserName/$developerGithubProjectName"
                        connection "scm:git:git://github.com/$developerGithubUserName/${developerGithubProjectName}.git"
                        developerConnection "scm:git:ssh:git@github.com:$developerGithubUserName/${developerGithubProjectName}.git"
                    }
                    licenses {
                        license {
                            name projectLicense
                            url projectLicenseUrl
                            distribution 'repo'
                        }
                    }
                    developers {
                        developer {
                            id developerGithubUserName
                            name developerFullName
                            email developerEmail
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = developerBintrayUserName
    key = System.getenv('BINTRAY_API_KEY')
    publications = ['maven']
    pkg {
        repo = 'kotlinx'
        name = projectName
        desc = projectDescription
        licenses = [projectLicenseTag]
        publicDownloadNumbers = true
        websiteUrl = "https://github.com/$developerGithubUserName/$developerGithubProjectName"
        issueTrackerUrl = "https://github.com/$developerGithubUserName/$developerGithubProjectName/issues"
        vcsUrl = "https://github.com/$developerGithubUserName/${developerGithubProjectName}.git"
        githubRepo = "$developerGithubUserName/$developerGithubProjectName"
        version {
            name = projectVersion
            vcsTag = projectVersion
            gpg {
                sign = true
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url "http://dl.bintray.com/kotlin/kotlinx.html/"
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlinVersion"
    compile 'org.jetbrains.kotlinx:kotlinx-html-js:0.6.10'

    testCompile "org.jetbrains.kotlin:kotlin-test-js:$kotlinVersion"
}
